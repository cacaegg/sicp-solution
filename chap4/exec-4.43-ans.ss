(define (require p)
  (if (not p) (amb)))

(define (xor-require p1 p2)
  (cond ((and p1 p2) (amb))
        ((and (not p1) (not p2)) (amb))))

(define (distinct? items)
  (cond ((null? items) true)
        ((null? (cdr items)) true)
        ((member (car items) (cdr items))
         false)
        (else (distinct? (cdr items)))))

(define daughter car)
(define yacht cdr)

(define (solve)
  (define (lookup-by-daughter records daughter-name)
    (cond ((null? records) (amb))
          ((eq? (daughter (car records)) daughter-name)
           (car records))
          (else (lookup-by-daughter (cdr records) daughter-name))))
  (define (all-daughter ls)
    (if (null? ls) 
        '() 
        (cons (daughter (car ls)) 
              (all-daughter (cdr ls)))))
  (define (all-yacht ls)
    (if (null? ls) 
        '() 
        (cons (yacht (car ls)) 
              (all-yacht (cdr ls)))))
  (define (same-daugter-yacht? record)
    (eq? (yacht record) (daughter record)))
  (let ((moore (cons (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa)
                     (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa))))
    (require (eq? (yacht moore) 'lorna))
    (require (eq? (daughter moore) 'mary))
    (require (not (same-daugter-yacht? moore)))
    (let ((downing (cons (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa)
                         (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa))))
      (require (eq? (yacht downing) 'melissa))
      (require (not (same-daugter-yacht? downing)))
      (let ((hall (cons (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa)
                        (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa))))
        (require (eq? (yacht hall) 'rosalind))
        (require (not (same-daugter-yacht? hall)))
        (let ((barnacle (cons (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa)
                              (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa))))
          (require (eq? (yacht barnacle) 'gabrielle))
          (require (eq? (daughter barnacle) 'melissa))
          (require (not (same-daugter-yacht? barnacle)))
          (let ((parker (cons (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa)
                              (amb 'mary 'gabrielle 'lorna 'rosalind 'melissa))))
            (require (not (same-daugter-yacht? parker)))
            (require (eq? 
                       (yacht (lookup-by-daughter 
                                (list moore downing hall barnacle parker) 
                                'gabrielle))
                       (daughter parker)))
            (require (distinct? (all-daughter (list moore downing hall barnacle parker))))
            (require (distinct? (all-yacht (list moore downing hall barnacle parker))))
            (list (cons 'moore moore) 
                  (cons 'downing downing) 
                  (cons 'hall hall)
                  (cons 'barnacle barnacle)
                  (cons 'parker parker))))))))

(solve)
try-again
try-again

;; 2 solutions if mary is not neccesary to be the daughter of moore
;; ((moore mary . lorna) (downing lorna . melissa) (hall gabrielle . rosalind) (barnacle melissa . gabrielle) (parker rosalind . mary))
;; ((moore gabrielle . lorna) (downing rosalind . melissa) (hall mary . rosalind) (barnacle melissa . gabrielle) (parker lorna . mary))
;;
;; 1 solution if mary is moore's daughter
; 
